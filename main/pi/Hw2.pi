module Hw2 where

-- First: read section 7.2 of the lecture notes about how 
-- propositional equality works in pi-forall. The key points are
-- that `Refl` is a proof of the identity type `(a = b)` when 
-- a is definitionally equal to b, and that `subst` is the elimination
-- form.

-- For example, we can show that equality is symmetric by 
-- eliminating pf (of type `x = y`) when type checking 
-- `Refl` against type `y = x`.  The `subst` adds the definition 
-- `x = y` to the context, so both sides of `y = x` wh normalize to y. 

sym : (A:Type) -> (x:A) -> (y:A) -> (x = y) -> y = x
sym = \ A x y pf .
  subst Refl by pf 

-- Homework: show that propositional equality is transitive 

trans : (A:Type) -> (x:A) -> (y:A) -> (z:A) -> (x = z) -> (z = y) -> (x = y)
trans = {- SOLN DATA -} \A x y z pf1 pf2 . 
  subst pf1 by pf2 {- STUBWITH TRUSTME -}

-- Homework: show that it is congruent for (nondependent) application

f_cong : (A:Type) -> (B : Type) -> (f : A -> B) -> (g : A -> B)
       -> (x:A) -> (y:A) 
       -> (f = g) -> (x = y) -> (f x = g y)
f_cong = {- SOLN DATA -} \ A B f g x y pf1 pf2 . 
   subst (subst Refl by pf1) by pf2 {- STUBWITH TRUSTME -}

-- Homework: what does congruence for dependent application look like?
-- In other words, what if f and g above have a dependent type?

{- SOLN DATA -}
f_cong_dep : (A:Type) -> (B : A -> Type) 
       -> (f : (x:A) -> B x) -> (g : (x:A) -> B x)
       -> (x:A) -> (y:A) 
       -> (f = g) -> (p : x = y) -> (f x = subst g y by p)
f_cong_dep =  \ A B f g x y pf1 pf2 . 
   subst (subst Refl by pf1) by pf2 {- STUBWITH -}


-- properties of booleans

-- an encoding of logical falsity

void : Type 
void = (A:Type) -> A

neg : Type -> Type
neg = \ A . ( (A) -> void )

not : Bool -> Bool 
not = \ x . if x then False else True

-- show that true is not false

not_not_equal : (b : Bool) -> (b = not b) -> void
not_not_equal = {- SOLN DATA -} \b pf. 
  if b then (contra pf) else (contra pf) {- STUBWITH TRUSTME -}


not_false_then_true : (b : Bool) -> neg (b = False) -> b = True
not_false_then_true = {- SOLN DATA -} \b nb. 
  if b then Refl else nb Refl (b = True) {- STUBWITH TRUSTME -}

-- show that decidable equality for booleans is correct.

eq_bool : Bool -> Bool -> Bool
eq_bool = \x y. if x then y else not y

eq_true : (b1 : Bool) -> (b2 : Bool) -> eq_bool b1 b2 = True -> (b1 = b2)
eq_true = \b1 b2 pf. {- SOLN DATA -}
  if b1 then if b2 then Refl else contra pf
  		  else if b2 then contra pf else Refl {- STUBWITH TRUSTME -}

eq_false : 	(b1 : Bool) -> (b2 : Bool) -> eq_bool b1 b2 = False -> (b1 = b2) -> void
eq_false = {- SOLN DATA -} \ b1 b2 pf1 pf2 .
  if b1 then if b2 then contra pf1 else contra pf2
        else if b2 then contra pf2 else contra pf1 {- STUBWITH TRUSTME -}

false_eq_bool : (n : Bool) -> (m : Bool) ->
     neg (n = m) ->
     eq_bool n m = False
false_eq_bool = \n m nnm. {- SOLN DATA -}
   if n then if m then nnm Refl (eq_bool n m = False)
                  else Refl
        else if m then Refl
                  else nnm Refl (eq_bool n m = False) {- STUBWITH TRUSTME -}





