-- Simple examples demonstrating parametric polymorphism in core language

module Lec1 where

id : (x:Type) -> x -> x
id = \x y . y 

idid : ((x:Type) -> (y : x) -> x) 
idid = id ((x:Type) -> (y : x) -> x) id

compose : (A : Type) -> (B : Type) -> (C:Type) ->
  (B -> C) -> (A -> B) -> (A -> C)
compose = \ A B C f g x. (f (g x))

idT : Type
idT = (x:Type) -> x -> x

selfapp : idT -> idT
selfapp = (\x.x : (idT -> idT) -> (idT -> idT)) (\x.x)

-- Church encoding: booleans

true : (A:Type) -> A -> A -> A 
true = \A x y. x

false : (A:Type) -> A -> A -> A 
false = \A x y. y

cond : ((A:Type) -> A -> A -> A) -> (x:Type) -> x -> x -> x
cond = \ b . b 

-- void type

void : Type
void = (x:Type) -> x

-- inhabited by diverging term

loop : (x:Type) -> x
loop = \x. loop x

-- unit type 

unit : Type
unit = (x:Type) -> x -> x

-- this code only type checks with a definition of type equality that 
-- includes beta-equivalence/definitions (i.e. >= version2)

-- Church encoding of simply-typed pairs


pair : Type -> Type -> Type
pair = \p. \q. (c: Type) -> (p -> q -> c) -> c

prod : (p:Type) -> (q:Type) -> p -> q -> pair p q
prod = \p.\q. \x.\y. \c. \f. f x y

-- (p:Type) -> (q:Type) -> p -> q -> pair p q
-- (p:Type) -> (q:Type) -> p -> q -> ((c: Type) -> (p -> q -> c) -> c)

prod' : (p:Type) -> (q:Type) -> p -> q -> ((c: Type) -> (p -> q -> c) -> c)
prod' = prod

proj1 : (p:Type) -> (q:Type) -> pair p q -> p
proj1  = \p. \q. \a. a p (\x.\y.x)

proj2 : (p:Type) -> (q:Type) -> pair p q -> q
proj2  = \p. \q. \a. a q (\x.\y.y)

swap : (p:Type) -> (q:Type) -> pair p q -> pair q p
swap = \p. \q. \a. prod q p (proj2 p q a) (proj1 p q a)

