-- PI library for natural numbers
--
-- Some of these are adapted from Trellys examples
--
-- Author: Stephanie Weirich
--

module Nat where

import Equality

data Nat : Type where
  Zero
  Succ of (Nat)

is_zero : Nat -> Bool
is_zero = \ x . case x of 
  Zero -> True
  Succ n' -> False

pred : Nat -> Nat
pred = \n . case n of
  Zero -> Zero
  Succ n' -> n'

nat_eq : Nat -> Nat -> Bool
nat_eq = \ x y .
  case x of 
     Zero -> case y of
        Zero -> True
        Succ n -> False
     Succ m -> case y of 
        Zero -> False
        Succ n -> nat_eq m n

plus : Nat -> Nat -> Nat
plus = \ n m .
  case n of
    Zero -> m
    Succ pred -> Succ (plus pred m)

mult : Nat -> Nat -> Nat
mult = \ n m .
  case n of
    Zero -> Zero
    Succ pred -> plus m (mult pred m)


minus : Nat -> Nat -> Nat
minus = \n m .
   case n of
     Zero -> Zero
     Succ pred -> case m of
                    Zero -> n
                    Succ mpred -> minus pred mpred

-------------------------------------------------------
-- Properties of naturals:
-- * successor is monotone
-- * successor is injective
-- * constructors are discriminable

m_eq_n_Sm_eq_Sn : [m:Nat] -> [n:Nat] -> m = n -> (Succ m = Succ n)
m_eq_n_Sm_eq_Sn = \[m] [n] pf.
  subst Refl by pf

Sm_eq_Sn_m_eq_n : [m : Nat] -> [n : Nat] -> Succ m = Succ n -> m = n
Sm_eq_Sn_m_eq_n = \[m] [n] p.
  f_equal [Nat] [Nat] [pred] [Succ m] [Succ n] p

Z_neq_S : [n : Nat] -> Zero = Succ n -> ((A : Type) -> A)
Z_neq_S = \[n] p.
  let split = (\n. case n of Zero -> Unit; Succ m -> (A : Type) -> A : Nat -> Type)
  in coe [Unit] [(A : Type) -> A] (f_equal [Nat] [Type] [split] [Zero] [Succ n] p) ()
-- Z_neq_S = \[n] p. contra p

-------------------------------------------------------
-- Reasoning about datatypes

minus_same_zero : (n : Nat @0) -> ((minus^0) n n = 0) @ 1
minus_same_zero = \ n .
    case n of
      Zero -> Refl 
      Succ p -> (minus_same_zero p : (minus^0) p p = 0)

lemma_minus_zero_id : (n : Nat) -> (minus n 0 = n)
lemma_minus_zero_id =   \n . case n of 
    Zero -> Refl 
    Succ n' -> Refl 


lemma_minus_plus_id : (m : Nat) -> (n : Nat) -> ((minus (plus m n) m) = n)
lemma_minus_plus_id = \m n .
  case m of
     Zero -> lemma_minus_zero_id n
     Succ m' -> lemma_minus_plus_id m' n


plus_associates : (i:Nat) -> (j:Nat) -> (k:Nat) -> plus (plus i j) k = plus i (plus j k)
plus_associates = \ i j k .
  case i of
     -- `Refl` away `Zero`s on the left of `(+)`: (0+j)+k = j+k = 0+(j+k)
    Zero -> Refl
    -- associate `plus` in `Succ` of the inductive case:
    -- S ((i'+j)+k) = S (i'+(j+k))
    Succ i' -> 
      let ih = (plus_associates i' j k) in
		m_eq_n_Sm_eq_Sn [plus (plus i' j) k] [plus i' (plus j k)] ih
